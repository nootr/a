const STDIN  0
const STDOUT 1
const STDERR 2

const SYS_READ          0
const SYS_WRITE         1
const SYS_OPEN          2
const SYS_MMAP          9
const SYS_MUNMAP        11
const SYS_RT_SIGACTION  13
const SYS_RT_SIGRETURN  15
const SYS_SETITIMER     38
const SYS_EXIT          60

const O_RDONLY      0
const O_WRONLY      1
const PROT_READ     1
const PROT_WRITE    2
const MAP_PRIVATE   2
const MAP_ANONYMOUS 32

const ITIMER_REAL   0
const SIGALRM       14
const SA_SIGINFO    4
const SA_RESTORER   67108864  # 0x04000000


def puts: ptr s -> void
    # Prints a string and returns the write syscall exit-code
    STDOUT     # File descriptor
    s          # Pointer to string
    s strlen   # String length
    SYS_WRITE
    syscall 3


def error: ptr s -> void
    # Prints an error and returns the write syscall exit-code
    STDERR     # File descriptor
    s          # Pointer to string
    s strlen   # String length
    SYS_WRITE
    syscall 3


def exit: int code -> void
    # Exits using a given exit code
    code SYS_EXIT syscall 1


def raise: ptr message -> void
    # Prints a given error message and exits with exitcode 1
    message traceback


def assert: bool assertion, ptr message -> void
    # Raises when assertion is False
    assertion False = if
        message traceback


def open: ptr filename, char mode -> int
    # Opens a given file in (r)ead or (w)rite mode and returns a file descriptor.
    filename
    0

    mode 'r' = if
        O_RDONLY or
    mode 'w' = if
        O_WRONLY or

    0
    SYS_OPEN
    syscall 3

    dup 0 < if
        "Could not open file\n" raise


def read: int fd, ptr buf, int count -> int
    # Reads from a file descriptor and returns the number of bytes read or zero if EOF
    fd buf count SYS_READ syscall 3
    dup 0 < if
        "Could not read file\n" raise


def rt_sigaction: int signum, ptr act, ptr oldact, int sigsetsize -> int
    # Changes the action taken on a certain signal
    signum act oldact sigsetsize SYS_RT_SIGACTION syscall 4
    dup 0 != if
        "Could not change signal action\n" raise


inline rt_sigreturn: -> void
    # Return from signal handler and cleanup stack frame
    SYS_RT_SIGRETURN syscall 0
    "RT_SIGRETURN should not return\n" raise


const sigaction.handler   0   # ptr
const sigaction.flags     8   # int
const sigaction.restorer  16  # ptr
const sigaction.mask      24  # int
const SIGACTION_SIZE      32


def setitimer: int which, ptr new_val, ptr old_val -> int
    # Set the value of an interval timer
    which new_val old_val SYS_SETITIMER syscall 3
    dup 0 != if
        "Could not set the timer\n" raise


const itimerval.interval  0   # timeval
const itimerval.value     16  # timeval
const ITIMERVAL_SIZE      32

const timeval.sec   0   # int
const timeval.usec  8   # int
const TIMEVAL_SIZE  16


buffer _sigaction 32

def setup_signal_handler: int signal, ptr handler, ptr restorer -> int
    # Setup a signal handler for a certain signal
    handler     _sigaction sigaction.handler + setp
    0           _sigaction sigaction.mask + seti

    restorer 0 castp = if
        0       _sigaction sigaction.flags + seti
        0 castp _sigaction sigaction.restorer + setp
    else
        SA_SIGINFO SA_RESTORER or \
                    _sigaction sigaction.flags + seti
        restorer    _sigaction sigaction.restorer + setp

    signal _sigaction 0 castp 8 rt_sigaction


buffer _itimerval 32

def set_timer: int inter_secs, int inter_usecs, int val_secs, int val_usecs -> int
    # Sets the values of the internal timer
    inter_secs  _itimerval itimerval.interval + timeval.sec + seti
    inter_usecs _itimerval itimerval.interval + timeval.usec + seti
    inter_secs  _itimerval itimerval.value + timeval.sec + seti
    inter_usecs _itimerval itimerval.value + timeval.usec + seti

    ITIMER_REAL _itimerval 0 castp setitimer
