import lib.std


const WIDTH   256
const HEIGHT  256


# Pixel struct

const pixel.r     0
const pixel.g     8
const pixel.b     16
const PIXEL_SIZE  24

def create_pixel: int r, int g, int b -> ptr
    # Create a pixel
    PIXEL_SIZE malloc
    r over pixel.r + seti
    g over pixel.g + seti
    b over pixel.b + seti


# Point struct

const point.x     0
const point.y     8
const point.z     16
const POINT_SIZE  24

def create_point: int x, int y, int z -> ptr
    # Create a point
    POINT_SIZE malloc
    x over point.x + seti
    y over point.y + seti
    z over point.z + seti


# Vector struct

const vec.x     0
const vec.y     8
const vec.z     16
const VEC_SIZE  24

def create_vec: int x, int y, int z -> ptr
    # Create a vector
    VEC_SIZE malloc
    x over vec.x + seti
    y over vec.y + seti
    z over vec.z + seti

def copy_vec: ptr v -> ptr
    # Returns a copy of a given vector
    VEC_SIZE malloc
    v over VEC_SIZE memcpy

def vec_add: ptr a, ptr b -> ptr
    # Add two vectors
    # Note that this frees or alters the given vectors!
    a
    dup vec.x + dup derefi b vec.x + derefi + swap seti
    dup vec.y + dup derefi b vec.y + derefi + swap seti
    dup vec.z + dup derefi b vec.z + derefi + swap seti
    b free

def vec_scale: ptr v, int s -> ptr
    # Scale a vector up
    v
    dup vec.x + dup derefi s * swap seti
    dup vec.y + dup derefi s * swap seti
    dup vec.z + dup derefi s * swap seti

def vec_shrink: ptr v, int s -> ptr
    # Scale a vector down
    v
    dup vec.x + dup derefi s / swap seti
    dup vec.y + dup derefi s / swap seti
    dup vec.z + dup derefi s / swap seti

def vec_len_sqr: ptr v -> int
    # Returns the square of the length of a given vector
    v vec.x + derefi dup *
    v vec.y + derefi dup * +
    v vec.z + derefi dup * +

def vec_len: ptr v -> int
    # Returns the length of a given vector
    v vec_len_sqr sqrt


# Ray struct

const ray.origin    0
const ray.direction 8
const RAY_SIZE      16

def create_ray: ptr o, ptr d -> ptr
    # Create a ray
    RAY_SIZE malloc
    o over ray.origin +     setp
    d over ray.direction +  setp

def destroy_ray: ptr ray -> void
    # Frees a ray struct
    ray
    dup ray.origin +    derefp free
    dup ray.direction + derefp free
    free

def ray_at: ptr ray, int distance -> ptr
    # Returns the point of vector ray at a certain distance
    ray ray.direction + derefp copy_vec distance vec_scale
    ray ray.origin + derefp copy_vec vec_add


# Pathtracer

def render_pixel: int x, int y -> ptr
    # Calculate a single pixel
    x y 64 create_pixel


def main: ptr argv, int argc -> int
    # Print PPM metadata
    "P3 " puts
    WIDTH itos puts " " puts
    HEIGHT itos puts " " puts
    "256\n" puts

    # Render image
    0
    while dup HEIGHT <
        0
        while dup WIDTH <
            over over swap render_pixel
            dup pixel.r + derefi itos puts " " puts
            dup pixel.g + derefi itos puts " " puts
            dup pixel.b + derefi itos puts " " puts
            free
            1 +
        drop
        1 +

        "\n" puts
    0
